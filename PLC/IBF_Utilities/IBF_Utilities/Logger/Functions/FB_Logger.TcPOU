<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.11">
  <POU Name="FB_Logger" Id="{4ed2955e-dae2-0ed6-1ff9-576a887035a7}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK PUBLIC FB_Logger
VAR_STAT
	aLogHandlers : ARRAY[1..10] OF POINTER TO FB_LogHandlerBase; // The log handlers that process the incoming log messages
	bUnexpectedCall : BOOL; // FB_Logger should never be called. Use this bit to avoid flooding the log.
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[IF NOT bUnexpectedCall THEN
	THIS^.FATAL('Unexpected call to FB_Logger(). Do not call FB_Logger!');
	bUnexpectedCall := TRUE;
END_IF
]]></ST>
    </Implementation>
    <Folder Name="Private" Id="{da518ae9-ecc8-0bd9-2299-b83099ccf2a1}" />
    <Folder Name="Public" Id="{690da83f-5550-0084-3e92-f3004d90cd65}" />
    <Method Name="AddLogHandler" Id="{04ba087a-14eb-0f97-3a1f-a8392feff25e}" FolderPath="Public\">
      <Declaration><![CDATA[METHOD PUBLIC AddLogHandler : BOOL
VAR_IN_OUT
	fbLogHandler : FB_LogHandlerBase;
END_VAR
VAR
	i : USINT; // Iterator
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[AddLogHandler := TRUE;
FOR i := 1 TO 10 DO
	IF aLogHandlers[i] = ADR(fbLogHandler) THEN // Already added
		RETURN;
	ELSIF aLogHandlers[i] = 0 THEN // Found empty spot in the array, add the log handler
		aLogHandlers[i] := ADR(fbLogHandler);
		AddLogHandler := TRUE;
		DEBUG(CONCAT('Added log handler: ', fbLogHandler.sInstancePath));
		RETURN;
	END_IF
END_FOR
AddLogHandler := FALSE; // Failed to add loghandler
]]></ST>
      </Implementation>
    </Method>
    <Method Name="DEBUG" Id="{73f740e6-2a7d-0335-2d60-04f04456d0fa}" FolderPath="Public\">
      <Declaration><![CDATA[METHOD PUBLIC DEBUG // Add a message to the log with log level DEBUG 
(*
The DEBUG level is used for logging messages that help developers find out what went wrong
during a debugging session. While the specifics of what messages to log at the DEBUG level
is dependent on your application, you generally want to include detailed information that
can help developers troubleshoot an issue quickly. 
This can include variable state in the surrounding scope, or relevant error codes.
Unlike TRACE, DEBUG level logging can be turned on in production without making the application unusable,
but it should not be left on indefinitely to ensure the log is not flooded.
*)
VAR_INPUT
	sMsg : T_MaxString; // The message to be logged
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.SendToLogHandlers(sMsg, E_LogLevel.DEBUG);]]></ST>
      </Implementation>
    </Method>
    <Method Name="ERROR" Id="{56e0a7f0-dba1-0134-25e6-42b1ca5ad6dd}" FolderPath="Public\">
      <Declaration><![CDATA[METHOD PUBLIC ERROR // Add a message to the log with log level ERROR
(*
The ERROR log level is used to represent error conditions in a system that
prevent a specific operation from running, but the system itself can continue
working even if it is at a reduced level of functionality or performance.
Generally, ERROR logs should be investigated as soon as possible but they don't
carry the same urgency as FATAL messages since the system can continue working.
*)
VAR_INPUT
	sMsg : T_MaxString; // The message to be logged
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.SendToLogHandlers(sMsg, E_LogLevel.ERROR);]]></ST>
      </Implementation>
    </Method>
    <Method Name="FATAL" Id="{9dea5fac-8f6f-00f5-025c-86cdbbb5c16f}" FolderPath="Public\">
      <Declaration><![CDATA[METHOD PUBLIC FATAL // Add a message to the log with log level FATAL
(*
The FATAL log level annotates messages with the greatest severity.
It usually means that something critical is broken, and the system cannot continue to do any more
useful work without the intervention of an engineer. T
ypically, such entries are logged before the system is shut down ("famous last words").
*)
VAR_INPUT
	sMsg : T_MaxString; // The message to be logged
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.SendToLogHandlers(sMsg, E_LogLevel.FATAL);]]></ST>
      </Implementation>
    </Method>
    <Method Name="INFO" Id="{129ab2a5-c74e-0bad-1ca8-3aaada834c1f}" FolderPath="Public\">
      <Declaration><![CDATA[METHOD PUBLIC INFO // Add a message to the log with log level INFO
(*
INFO-level messages indicate events in the system that are significant to the
general purpose of the system. Such events are logged to show that the system is
operating normally. For example, a recipe was started or stopped. 
Production systems typically default to logging at this level so that a 
summary of the application's normal behavior is visible to anyone reading the logs.
*)
VAR_INPUT
	sMsg : T_MaxString; // The message to be logged
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.SendToLogHandlers(sMsg, E_LogLevel.INFO);]]></ST>
      </Implementation>
    </Method>
    <Method Name="Run" Id="{233b51c6-d710-06cd-118c-9fe291bd4fd8}" FolderPath="Public\">
      <Declaration><![CDATA[METHOD PUBLIC Run // Enables the logger and log handlers, should be called once every PLC cycle in your program/project (not multiple calls per cycle!)
VAR
	i : USINT; // Iterator
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[// Call every log handler. Only relevant for log handlers that take multiple PLC cycles
// to handle incoming log items (e.g., writing log items to disk).
FOR i := 1 TO 10 DO
	IF aLogHandlers[i] <> 0 THEN
		aLogHandlers[i]^();
	END_IF
END_FOR
]]></ST>
      </Implementation>
    </Method>
    <Method Name="SendToLogHandlers" Id="{1b8f16a4-be8c-02d0-187f-c86b3057e6cd}" FolderPath="Private\">
      <Declaration><![CDATA[METHOD PRIVATE SendToLogHandlers
VAR_INPUT
	sMsg : T_MaxString; // The message to be logged
	eLevel : E_LogLevel; // Log level (debug, info, warn, etc.)
END_VAR
VAR
	i : USINT; // Iterator
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[FOR i := 1 TO 10 DO
	IF aLogHandlers[i] <> 0 THEN
		aLogHandlers[i]^.M_AddToLog(sMsg, eLevel);
	END_IF
END_FOR
]]></ST>
      </Implementation>
    </Method>
    <Method Name="TRACE" Id="{852b99e3-4aae-02e1-20be-be286b738a87}" FolderPath="Public\">
      <Declaration><![CDATA[METHOD PUBLIC TRACE // Add a message to the log with log level TRACE
(*
The TRACE level can be used for tracing the path of code execution in a program.
Generally, TRACE is used for showing the flow of the program, and to provide a detailed breakdown of the sequence
of events that led to a crash, a silent failure, an error, or some other event logged at a different level.
The TRACE level is only used temporarily (when the code is a "work in progress").
*)
VAR_INPUT
	sMsg : T_MaxString; // The message to be logged
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.SendToLogHandlers(sMsg, E_LogLevel.TRACE);]]></ST>
      </Implementation>
    </Method>
    <Method Name="WARN" Id="{6d9befb4-3cad-0f4d-268e-38de03686e94}" FolderPath="Public\">
      <Declaration><![CDATA[METHOD PUBLIC WARN // Add a message to the log with log level WARN
(*
Messages logged at the WARN level typically indicate that something unexpected happened,
but the system can recover and continue to function normally.
It is mainly used to draw attention to situations that should be addressed soon before
they pose a problem for the system.
*)
VAR_INPUT
	sMsg : T_MaxString; // The message to be logged
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.SendToLogHandlers(sMsg, E_LogLevel.WARN);]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>